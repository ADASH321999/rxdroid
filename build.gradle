apply plugin: 'com.android.application'
//apply plugin: 'hugo'

def pwd = new File('.').absolutePath

buildscript {
	repositories {
		mavenCentral()
		maven {
			url 'https://maven.google.com/'
			name 'Google'
		}
		google()
		jcenter()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:3.2.1'
		//classpath 'com.jakewharton.hugo:hugo-plugin:1.1.0'
	}
}

repositories {
	mavenCentral()
	maven {
		url "file://$pwd/.m2repo"
	}
	maven {
		url 'https://maven.google.com/'
		name 'Google'
	}
	jcenter()
	maven {
		url "https://jitpack.io"
	}
}

dependencies {
		implementation 'com.j256.ormlite:ormlite-core:4.48'
		implementation 'com.j256.ormlite:ormlite-android:4.48'
		implementation 'com.android.support:support-v4:26.0.0'
		implementation 'com.android.support:support-v13:26.0.0'
		implementation 'com.android.support:appcompat-v7:26.0.0'
		//compile 'com.android.support:cardview-v7:21.0.0'
		implementation 'com.nineoldandroids:library:2.4.0'
		implementation 'net.lingala.zip4j:zip4j:1.3.2'
		//compile 'ch.acra:acra:5.0.0-SNAPSHOT'
        implementation 'joda-time:joda-time:2.5'

		implementation 'at.jclehner.rxdroid.localmaven:ShowcaseView:9999'
		//compile 'com.github.deano2390:MaterialShowcaseView:1.2.0'
}


android {
	buildToolsVersion '28.0.3'
	compileSdkVersion 28

	defaultConfig {
		minSdkVersion 14
		targetSdkVersion 28

        applicationId 'at.jclehner.rxdroid';

		versionCode 9321
		versionName '0.9.32.1'
	}

	lintOptions {
		warning 'MissingTranslation'
	}

	signingConfigs {
		release {
			def keystore = System.getenv("RXDROID_KEYSTORE")
			if (keystore) {
				storeFile file(keystore)
				keyAlias System.getenv("RXDROID_KEY_ALIAS")
				storePassword "****"
				keyPassword "****"
			}
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFile 'proguard-project.txt'
			signingConfig signingConfigs.release
		}
		debug {
			applicationIdSuffix '.debug'
		}
	}
}


task askForKeystorePassword {
	doLast {
		if (!android.signingConfigs.release.storeFile) {
			return
		}

		def prompt = 
				"\n==========================================================" +
				"\nKeystore : ${android.signingConfigs.release.storeFile}"     +
				"\nKey alias: ${android.signingConfigs.release.keyAlias}"      +
				"\n==========================================================" +
				"\nPassword: "

		def console = System.console()

		if (console) {
			def pw = new String(console.readPassword(prompt))

			android.signingConfigs.release.storePassword = pw
			android.signingConfigs.release.keyPassword = pw
		}
	}
}

tasks.whenTaskAdded { theTask ->
	if (theTask.name.equals("packageRelease")) {
		theTask.dependsOn "askForKeystorePassword"
	}
}
