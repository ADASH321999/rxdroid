apply plugin: 'android'

buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:0.9.+'
		//classpath 'com.android.tools.build:gradle:0.6.3+'
	}
}

repositories {
	mavenCentral()
	maven {
		url './.m2repo'
	}
}

dependencies {
		//compile 'com.google.code.gson:gson:2.2.4'
		compile 'com.j256.ormlite:ormlite-core:4.47'
		compile 'com.j256.ormlite:ormlite-android:4.47'
		compile 'com.android.support:support-v4:19.0.0'
		compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
		compile 'com.nineoldandroids:library:2.4.0'
		compile 'net.lingala.zip4j:zip4j:1.3.2'
	
		//compile 'at.jclehner.rxdroid.localmaven:SherlockNavigationDrawer:1.0'
		compile 'at.jclehner.rxdroid.localmaven:NumberPicker:9999'
		compile 'at.jclehner.rxdroid.localmaven:DragSortListView:9999'
		compile 'at.jclehner.rxdroid.localmaven:ShowcaseView:9999'
}


android {
	buildToolsVersion '19.0.1'
	compileSdkVersion 19

	signingConfigs {
		release {
			def keystore = System.getenv("RXDROID_KEYSTORE")
			if (keystore) {
				storeFile file(keystore)
				keyAlias System.getenv("RXDROID_KEY_ALIAS")
				storePassword "****"
				keyPassword "****"
			}
		}
	}

	buildTypes {
		release {
			runProguard false
			proguardFile getDefaultProguardFile('proguard-android.txt')
			proguardFile 'proguard-project.txt'
			signingConfig signingConfigs.release
		}
	}
}


task askForKeystorePassword << {
	def prompt = 
			"\n==========================================================" +
			"\nKeystore : ${android.signingConfigs.release.storeFile}"     +
			"\nKey alias: ${android.signingConfigs.release.keyAlias}"      +
			"\n==========================================================" +
			"\nPassword: "

	def console = System.console()
	
	if (console) {
		def pw = new String(console.readPassword(prompt))

		android.signingConfigs.release.storePassword = pw
		android.signingConfigs.release.keyPassword = pw
	}
}

tasks.whenTaskAdded { theTask ->
	if (theTask.name.equals("packageRelease")) {
		theTask.dependsOn "askForKeystorePassword"
	}
}
